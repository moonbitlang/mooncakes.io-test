<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="UTF-8">
  <title>Moonbit docs</title>
  <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
  <meta name="description" content="Description">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, minimum-scale=1.0">
  <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/styles/base16/one-light.css">
  <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/highlight.min.js"></script>
  <script
    src="//cdnjs.cloudflare.com/ajax/libs/highlightjs-line-numbers.js/2.8.0/highlightjs-line-numbers.min.js"></script>
</head>

<body>
  <pre><code class="moonbit">///|
pub enum Msg {
  DeleteAll
  GotCardsData(Result[Array[CardModel], String])
  ReqResult(Result[Json, String])
} derive(Show)

///|
pub struct Model {
  cards : Array[CardModel]
} derive(Show)

///|
pub fn load() -> (@tea.Cmd[Msg], Model) {
  (
    @http.get("/api/cards", expect=Json(Msg::GotCardsData, decode_cards)),
    { cards: [] },
  )
}

///|
pub fn view(model : Model) -> @html.Html[Msg] {
  @html.div(style=["p-0", "display:flex", "flex-direction:column"], [
    @html.h1([@html.text("MoonBit TodoMVC")]),
    @html.div(style=["display: flex", "width: 500px", "height: 100%"], [
      @views.button("clear", click=DeleteAll),
      @views.button("+", href="/new"),
    ]),
    @html.p([@html.text("Click cards below to edit, click + to add a new card")]),
    view_list(model.cards),
  ])
}

///|
pub fn update(msg : Msg, model : Model) -> (@tea.Cmd[Msg], Model) {
  match msg {
    DeleteAll => {
      let cmds = @tea.batch(
        [
          ..model.cards.map(fn(card) {
            @http.delete(
              "/api/cards/\{card.id()}",
              expect=Json(Msg::ReqResult, Result::Ok),
            )
          }),
          @nav.push_url("/home"),
        ],
      )
      (cmds, model)
    }
    GotCardsData(res) =>
      match res {
        Ok(cards) => (@tea.none(), { cards, })
        Err(_) => (@tea.none(), { cards: [] })
      }
    ReqResult(_) => (@tea.none(), model)
  }
}

///|
fn decode_cards(json : Json) -> Result[Array[CardModel], String] {
  match json {
    { "cards": Array(elems), .. } => {
      let cards = elems.filter_map(fn {
        {
          "title"? : Some(String(""))
          | None,
          "content": String(content),
          "id": Number(id),
          ..
        } => Some(Card(content, id=id.to_int()))
        {
          "title": String(title),
          "content": String(content),
          "id": Number(id),
          ..
        } => Some(TitleCard(title~, content~, id=id.to_int()))
        _ => None
      })
      Ok(cards)
    }
    _ => Err("Invalid cards data")
  }
}
</code></pre>
  <script>
    let moonbitLanguageFn = hljs => {
      return {
        case_insensitive: true,
        keywords: {
          keyword: 'func fn enum struct type if else match return continue break while let var interface pub priv readonly',
          literal: 'true false',
          type: "Int Int64 Double String Bool Char Bytes Option Array Result",
          built_in: 'lsl lsr asr shl shr land lor lxor Show Debug Hash Eq Compare Some None'
        },
        contains: [
          {
            scope: "char",
            begin: "'", end: "'"
          },
          {
            scope: "string",
            begin: "\"", end: "\""
          },
          {
            scope: "number",
            begin: "\\b\\d+(\\.\\d+)?\\b"
          },
          {
            scope: "codelink",
            match: /\<a href\="(?<link>[^<>]+?)"\>(?<code>[^\/<>]+?)\<\/a\>/g
          },
          hljs.COMMENT(
            '//', // begin
            '\n', // end
          )
        ]
      }
    }

    hljs.registerLanguage('moonbit', moonbitLanguageFn);
    hljs.highlightAll();
    hljs.initLineNumbersOnLoad();

    const number = window.location.href.split('#')[1];

    function waitForLineNumbers() {
      setTimeout(function () {
        const target = document.querySelector(`.hljs-ln-line[data-line-number="${number}"]`);
        if (target == null) waitForLineNumbers();
        else target.scrollIntoView();
      }, 50);
    }

    waitForLineNumbers()

  </script>
  <style>
    .hljs-ln-numbers {
      -webkit-touch-callout: none;
      -webkit-user-select: none;
      -khtml-user-select: none;
      -moz-user-select: none;
      -ms-user-select: none;
      user-select: none;
    }

    .hljs-ln-n {
      color: #ccc;
      border-right: 1px solid #dfdddd;
      margin-right: 1em;
      text-align: center;
      vertical-align: top;
      padding-right: 0.5em;
    }

    .hljs {
      background: none;
    }

    body {
      background-color: #fafafa;
    }
  </style>
</body>

</html>