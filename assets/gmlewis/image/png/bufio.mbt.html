<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="UTF-8">
  <title>Moonbit docs</title>
  <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
  <meta name="description" content="Description">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, minimum-scale=1.0">
  <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/styles/base16/one-light.css">
  <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/highlight.min.js"></script>
  <script
    src="//cdnjs.cloudflare.com/ajax/libs/highlightjs-line-numbers.js/2.8.0/highlightjs-line-numbers.min.js"></script>
</head>

<body>
  <pre><code class="moonbit">///|
/// This file is based on the Go implementation found here:
/// https://cs.opensource.google/go/go/+/refs/tags/go1.23.3:src/bufio.go
/// which has the copyright notice:
/// Copyright 2009 The Go Authors. All rights reserved.
/// Use of this source code is governed by a BSD-style
/// license that can be found in the LICENSE file.
///
/// Package bufio implements buffered I/O. It wraps an io.Reader or io.Writer
/// object, creating another object (Reader or Writer) that also implements
/// the interface but provides buffering and some help for textual I/O.
const DEFAULT_BUF_SIZE = 4096

///|
/// ReaderSize implements buffering for an io.Reader object.
priv struct ReaderSize {
  mut buf : Slice[Byte]
  // reader provided by the client
  mut rd : &@io.Reader
  // buf read and write positions
  mut r : Int
  mut w : Int
  mut err : IOError?
}

///|
const MIN_READ_BUFFER_SIZE = 16

///|
const MAX_CONSECUTIVE_EMPTY_READS = 100

///|
/// `ReaderSize::new` returns a new [Reader] whose buffer has at least the specified
/// size. If the argument io.Reader is already a [Reader] with large enough
/// size, it returns the underlying [Reader].
fn ReaderSize::new(rd : &@io.Reader, size : Int) -> ReaderSize {
  // Is it already a Reader?
  // b, ok := rd.(*Reader)
  // if ok && len(b.buf) >= size {
  // 	return b
  // }
  let r = { buf: Slice::new([]), rd, r: 0, w: 0, err: None }
  // lastRuneSize: -1,
  r.reset_buf(
    Slice::new(Array::make(@math.maximum(size, MIN_READ_BUFFER_SIZE), b'\x00')),
    rd,
  )
  r
}

///|
fn reset_buf(self : ReaderSize, buf : Slice[Byte], r : &@io.Reader) -> Unit {
  self.buf = buf
  self.rd = r
}

///|
pub let err_negative_read : IOError = IOError(
  "bufio: reader returned negative count from read",
)

///|
/// fill reads a new chunk into the buffer.
fn fill(self : ReaderSize) -> IOError? {
  // Slide existing data to beginning.
  if self.r > 0 {
    let _ = slice_copy(self.buf, self.buf[self.r:self.w])
    self.w -= self.r
    self.r = 0
  }
  if self.w >= self.buf.length() {
    return Some(IOError("bufio: tried to fill full buffer"))
  }

  // Read new data: try a limited number of times.
  for i = MAX_CONSECUTIVE_EMPTY_READS; i > 0; i = i - 1 {
    let (n, err) = self.rd.read(self.buf[self.w:])
    if n < 0 {
      return Some(err_negative_read)
    }
    self.w += n
    if err != None {
      self.err = err
      return None
    }
    if n > 0 {
      return None
    }
  }
  Some(@io.err_no_progress)
}

///|
fn read_err(self : ReaderSize) -> IOError? {
  let err = self.err
  self.err = None
  err
}

///|
/// read reads data into p.
/// It returns the number of bytes read into p.
/// The bytes are taken from at most one Read on the underlying [Reader],
/// hence n may be less than len(p).
/// To read exactly len(p) bytes, use io.ReadFull(b, p).
/// If the underlying [Reader] can return a non-zero count with io.EOF,
/// then this Read method can do so as well; see the [io.Reader] docs.
impl @flate.Reader for ReaderSize with read(self, p) {
  let mut n = p.length()
  if n == 0 {
    if self.buffered() > 0 {
      return (0, None)
    }
    return (0, self.read_err())
  }
  if self.r == self.w {
    if self.err != None {
      return (0, self.read_err())
    }
    if p.length() >= self.buf.length() {
      // Large read, empty buffer.
      // Read directly into p to avoid copy.
      let tmp = self.rd.read(p)
      n = tmp.0
      self.err = tmp.1
      if n < 0 {
        return (0, Some(err_negative_read))
      }
      return (n, self.read_err())
    }
    // One read.
    // Do not use self.fill, which will loop.
    self.r = 0
    self.w = 0
    let tmp = self.rd.read(self.buf)
    n = tmp.0
    self.err = tmp.1
    if n < 0 {
      return (0, Some(err_negative_read))
    }
    if n == 0 {
      return (0, self.read_err())
    }
    self.w += n
  }

  // copy as much as we can
  // Note: if the slice panics here, it is probably because
  // the underlying reader returned a bad count. See issue 49795.
  n = slice_copy(p, self.buf[self.r:self.w])
  self.r += n
  (n, None)
}

///|
/// read_byte reads and returns a single byte.
/// If no byte is available, returns an error.
impl @flate.Reader for ReaderSize with read_byte(self) {
  // self.lastRuneSize = -1
  while self.r == self.w {
    if self.err != None {
      return (0, self.read_err())
    }
    self.err = self.fill() // buffer is empty
  }
  let c = self.buf[self.r]
  self.r += 1
  (c, None)
}

///|
/// Buffered returns the number of bytes that can be read from the current buffer.
fn buffered(self : ReaderSize) -> Int {
  self.w - self.r
}
</code></pre>
  <script>
    let moonbitLanguageFn = hljs => {
      return {
        case_insensitive: true,
        keywords: {
          keyword: 'func fn enum struct type if else match return continue break while let var interface pub priv readonly',
          literal: 'true false',
          type: "Int Int64 Double String Bool Char Bytes Option Array Result",
          built_in: 'lsl lsr asr shl shr land lor lxor Show Debug Hash Eq Compare Some None'
        },
        contains: [
          {
            scope: "char",
            begin: "'", end: "'"
          },
          {
            scope: "string",
            begin: "\"", end: "\""
          },
          {
            scope: "number",
            begin: "\\b\\d+(\\.\\d+)?\\b"
          },
          {
            scope: "codelink",
            match: /\<a href\="(?<link>[^<>]+?)"\>(?<code>[^\/<>]+?)\<\/a\>/g
          },
          hljs.COMMENT(
            '//', // begin
            '\n', // end
          )
        ]
      }
    }

    hljs.registerLanguage('moonbit', moonbitLanguageFn);
    hljs.highlightAll();
    hljs.initLineNumbersOnLoad();

    const number = window.location.href.split('#')[1];

    function waitForLineNumbers() {
      setTimeout(function () {
        const target = document.querySelector(`.hljs-ln-line[data-line-number="${number}"]`);
        if (target == null) waitForLineNumbers();
        else target.scrollIntoView();
      }, 50);
    }

    waitForLineNumbers()

  </script>
  <style>
    .hljs-ln-numbers {
      -webkit-touch-callout: none;
      -webkit-user-select: none;
      -khtml-user-select: none;
      -moz-user-select: none;
      -ms-user-select: none;
      user-select: none;
    }

    .hljs-ln-n {
      color: #ccc;
      border-right: 1px solid #dfdddd;
      margin-right: 1em;
      text-align: center;
      vertical-align: top;
      padding-right: 0.5em;
    }

    .hljs {
      background: none;
    }

    body {
      background-color: #fafafa;
    }
  </style>
</body>

</html>