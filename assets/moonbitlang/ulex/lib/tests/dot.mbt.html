<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="UTF-8">
  <title>Moonbit docs</title>
  <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
  <meta name="description" content="Description">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, minimum-scale=1.0">
  <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/styles/base16/one-light.css">
  <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/highlight.min.js"></script>
  <script
    src="//cdnjs.cloudflare.com/ajax/libs/highlightjs-line-numbers.js/2.8.0/highlightjs-line-numbers.min.js"></script>
</head>

<body>
  <pre><code class="moonbit">///|
type Edge (Int, Int, String?) // from, to, edge_info

///|
enum NodeType {
  Normal
  Accepted
}

///|
pub(all) struct DotGraph {
  edges : Array[Edge]
  nodes : Map[Int, (NodeType, String?)]
  name : String
}

///|
pub fn DotGraph::new(name~ : String = "graph") -> DotGraph {
  { edges: [], nodes: Map::new(), name }
}

///|
pub fn add_edge(
  self : DotGraph,
  from~ : Int,
  to~ : Int,
  edge_info? : String
) -> Unit {
  self.edges.push((from, to, edge_info))
}

///|
fn set_node(
  self : DotGraph,
  node~ : Int,
  nodeType~ : NodeType,
  label? : String
) -> Unit {
  self.nodes.set(node, (nodeType, label))
}

///|
fn to_graphviz(self : DotGraph) -> String {
  [
    "digraph \{self.name} {",
    self.edges.map(fn(e) {
      let (from, to, info) = e._
      "\t\{from} -> \{to}" + info.map_or("", fn(t) { "[label=\{t}]" })
    })
    |> @string.concat(separator="\n"),
    self.nodes
    .iter()
    .map(fn {
      (node, (nodeType, label)) =>
        [
          node.to_string(),
          "[",
          "shape=",
          match nodeType {
            Normal => "circle"
            Accepted => "box"
          },
          ",",
          "label=",
          label.or(node.to_string()),
          "]",
        ]
        |> @string.concat()
    })
    .collect()
    |> @string.concat(separator="\n"),
    "}",
  ]
  |> @string.concat()
}

///|
fn to_mermaid(self : DotGraph) -> String {
  let styles =
    #|---
    #|  config:
    #|    look: handDrawn
    #|---
  [
    "```mermaid",
    styles,
    "graph TD",
    self.edges.map(fn(e) {
      let (from, to, edge_info) = e._
      from.to_string() +
      "-->" +
      edge_info.map_or("", fn(s) { "|" + s + "|" }) +
      to.to_string()
    })
    |> @string.concat(separator="\n"),
    self.nodes
    .iter()
    .map(fn(n) {
      let (node, (nodeType, label)) = n
      match nodeType {
        Normal => "\{node}((\{label.or(node.to_string())}))"
        Accepted => "\{node}[\{label.or(node.to_string())}]"
      }
    })
    .collect()
    |> @string.concat(separator="\n"),
    "```",
  ]
  |> @string.concat(separator="\n")
}
</code></pre>
  <script>
    let moonbitLanguageFn = hljs => {
      return {
        case_insensitive: true,
        keywords: {
          keyword: 'func fn enum struct type if else match return continue break while let var interface pub priv readonly',
          literal: 'true false',
          type: "Int Int64 Double String Bool Char Bytes Option Array Result",
          built_in: 'lsl lsr asr shl shr land lor lxor Show Debug Hash Eq Compare Some None'
        },
        contains: [
          {
            scope: "char",
            begin: "'", end: "'"
          },
          {
            scope: "string",
            begin: "\"", end: "\""
          },
          {
            scope: "number",
            begin: "\\b\\d+(\\.\\d+)?\\b"
          },
          {
            scope: "codelink",
            match: /\<a href\="(?<link>[^<>]+?)"\>(?<code>[^\/<>]+?)\<\/a\>/g
          },
          hljs.COMMENT(
            '//', // begin
            '\n', // end
          )
        ]
      }
    }

    hljs.registerLanguage('moonbit', moonbitLanguageFn);
    hljs.highlightAll();
    hljs.initLineNumbersOnLoad();

    const number = window.location.href.split('#')[1];

    function waitForLineNumbers() {
      setTimeout(function () {
        const target = document.querySelector(`.hljs-ln-line[data-line-number="${number}"]`);
        if (target == null) waitForLineNumbers();
        else target.scrollIntoView();
      }, 50);
    }

    waitForLineNumbers()

  </script>
  <style>
    .hljs-ln-numbers {
      -webkit-touch-callout: none;
      -webkit-user-select: none;
      -khtml-user-select: none;
      -moz-user-select: none;
      -ms-user-select: none;
      user-select: none;
    }

    .hljs-ln-n {
      color: #ccc;
      border-right: 1px solid #dfdddd;
      margin-right: 1em;
      text-align: center;
      vertical-align: top;
      padding-right: 0.5em;
    }

    .hljs {
      background: none;
    }

    body {
      background-color: #fafafa;
    }
  </style>
</body>

</html>